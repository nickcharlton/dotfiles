#
# A custom prompt theme by nickcharlton.
#

# Load dependencies.
pmodload 'helper'

function prompt_nickcharlton_pwd {
  local pwd="${PWD/#$HOME/~}"

  if [[ "$pwd" == (#m)[/~] ]]; then
    _prompt_nickcharlton_pwd="$MATCH"
    unset MATCH
  else
    _prompt_nickcharlton_pwd="${${${(@j:/:M)${(@s:/:)pwd}##.#?}:h}%/}/${pwd:t}"
  fi
}

function prompt_nickcharlton_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # working directory segment
  prompt_nickcharlton_pwd

  # vcs status segment
  vcs_info
}

function prompt_nickcharlton_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # add the precommand hook
  add-zsh-hook precmd prompt_nickcharlton_precmd

  # format strings
  local dirty_format="%F{yellow}"
  local branch_format="%F{cyan}%c%u(%b)%f"
  local action_format="${branch_format}%F{red}[%a]{%f}"

  # configure vcs_info
  zstyle ':vcs_info:*' enable git
  zstyle ':vcs_info:*' check-for-changes true
  zstyle ':vcs_info:*' stagedstr "${dirty_format}"
  zstyle ':vcs_info:*' unstagedstr "${dirty_format}"
  zstyle ':vcs_info:*' formats "${branch_format}"
  zstyle ':vcs_info:*' actionformats "${action_format}"

  # define prompts.
  PROMPT='[%n@%m:%F{cyan}${_prompt_nickcharlton_pwd}%f]${vcs_info_msg_0_}%# '
  RPROMPT='${editor_info[overwrite]}%(?:: %F{red}‚èé%f)'
  SPROMPT='zsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? '
}

prompt_nickcharlton_setup "$@"

